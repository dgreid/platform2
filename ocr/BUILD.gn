# Copyright 2020 The Chromium OS Authors.  All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/mojom_bindings_generator.gni")
import("//common-mk/pkg_config.gni")

# The target named "all" is the root target built
# by the platform2 build system.
group("all") {
  deps = [ ":ocr_service" ]
  if (use.test) {
    deps += [ ":ocr_service_test" ]
  }
}

generate_mojom_bindings("ocr_mojo_bindings") {
  mojo_root = "${platform2_root}"
  sources = [ "mojo/ocr_service.mojom" ]
}

pkg_config("target_defaults") {
  cflags_c = [
    "-Wextra",
    "-Wshadow",
  ]
  cflags_cc = [ "-Woverloaded-virtual" ]
  pkg_deps = [
    "libbrillo",
    "libchrome-${libbase_ver}",
    "libmojo-${libbase_ver}",
    "system_api",
    "tesseract",
  ]
}

source_set("libocr") {
  configs += [ ":target_defaults" ]
  deps = [ ":ocr_mojo_bindings" ]
  sources = [
    "daemon.cc",
    "ocr_service_impl.cc",
  ]
}

source_set("libocr_mojo_adapter") {
  configs += [ ":target_defaults" ]
  deps = [ ":ocr_mojo_bindings" ]
  sources = [
    "mojo_adapter/ocr_service_mojo_adapter.cc",
    "mojo_adapter/ocr_service_mojo_adapter_delegate_impl.cc",
  ]
}

executable("ocr_service") {
  configs += [ ":target_defaults" ]
  deps = [ ":libocr" ]
  sources = [ "main.cc" ]
}

if (use.test) {
  executable("ocr_service_test") {
    configs += [
      "//common-mk:test",
      ":target_defaults",
    ]
    deps = [
      ":libocr",
      "//common-mk/testrunner:testrunner",
    ]
    sources = [ "ocr_service_impl_test.cc" ]
  }
}
